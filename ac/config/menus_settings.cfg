newmenu setup

menuitemtextinput "Nickname:			" "result $curname" "name $arg1"
menuitemslider "Team: " 0 1 "(curteam)" 1 "CLA RVSF" "alias newteam $arg1"
menuitem "" ""
menuitem "Select skin.."          [showmenu (at [CLA RVSF] (curteam))]
menuitem "Select weapon.."        "showmenu weapons"
menuitem "Select crosshair..				"	  "showmenu Crosshairs"
menuitem "" ""
menuitem "[ OK ]" [team (at [CLA RVSF] $newteam)]

newmenu Crosshairs
menudirlist "packages/misc/crosshairs" "png" "loadcrosshair (concatword $arg1 .png)"


newmenu weapons

menumdl weapons/assault/menu mapmodel 75 12
menuitem "MTP-57 Assault Rifle"			"nextprimary 5;echo MTP-57 Assault Rifle chosen" "chmenumdl weapons weapons/assault/menu mapmodel 75 12"
menuitem "A-ARD/10 Submachine Gun"		"nextprimary 3;echo A-ARD/10 Submachine Gun chosen" "chmenumdl weapons weapons/subgun/menu mapmodel 75 12"
menuitem "Precision Tech AD-81 Sniper Rifle"     "nextprimary 4;echo Precision Tech AD-81 Sniper Rifle chosen" "chmenumdl weapons weapons/sniper/menu mapmodel 75 12"
menuitem "V-19 Combat Shotgun"			"nextprimary 2;echo V-19 Combat Shotgun chosen" "chmenumdl weapons weapons/shotgun/menu mapmodel 75 12"


newmenu CLA

menumdl playermodels/CLA/01 all 50 4
menuitem "Comandante"           "skin 0" "chmenumdl CLA playermodels/CLA/01 all 50 4"
menuitem "Psycho"               "skin 1" "chmenumdl CLA playermodels/CLA/02 all 50 4"
menuitem "Bomber"               "skin 2" "chmenumdl CLA playermodels/CLA/03 all 50 4"
menuitem "Ripper"               "skin 3" "chmenumdl CLA playermodels/CLA/04 all 50 4"


newmenu RVSF

menumdl playermodels/RVSF/01 all 50 4
menuitem "Alpha"            "skin 0" "chmenumdl RVSF playermodels/RVSF/01 all 50 4"
menuitem "Tango"            "skin 1" "chmenumdl RVSF playermodels/RVSF/02 all 50 4"
menuitem "Cobra"            "skin 2" "chmenumdl RVSF playermodels/RVSF/03 all 50 4"
menuitem "Viper"            "skin 3" "chmenumdl RVSF playermodels/RVSF/04 all 50 4"
menuitem "Swat"             "skin 4" "chmenumdl RVSF playermodels/RVSF/05 all 50 4"
menuitem "Police"           "skin 5" "chmenumdl RVSF playermodels/RVSF/06 all 50 4"


newmenu settings

menuitem "Quick setup.."                                            [ showmenu "quick setup" ]
menuitem "Keyboard/mouse controls.."                                "showmenu controls"
menuitem "Video settings.."                                         "showmenu video"
menuitem "Sound settings.."                                         "showmenu sound"
menuitem "" ""
menuitem "Delete and reset all settings.."                              [ showmenu "Are you sure you want to delete/reset ALL settings?" ]

newmenu "quick setup"

menuitemtextinput "Nickname:					" "" "name $arg1"
menuitemtextinput "Resolution (Width):				" "$scr_w" "scr_w $arg1"
menuitemtextinput "Resolution (Height):				" "$scr_h" "scr_h $arg1"
menuitemslider "Mouse sensitivity: " 2 100 "($sensitivity)" 2 "" "sensitivity $arg1"
menuitemslider "Graphics detail: " 1 4 "(getgfxq)" 1 "low medium good best" "new_gfx_quality $arg1"
menuitemslider "Sound detail: " 1 3 "(getsoundq)" 1 "low medium high" "new_sound_quality $arg1"
menuitem "" ""
menuitem "				[ OK ]				" "resetgl"

alias getgfxq [
    if (= $minlod 200) [ result 4 ] [
        if (= $minlod 100) [ result 3 ] [
            if (= $minlod 60) [ result 2 ] [ result 1 ]
        ]
    ]
]

alias getsoundq [
    if (= $soundchans 64) [ result 3 ] [
        if (= $footsteps 1) [ result 2 ] [ result 1 ]
    ]
]

alias new_gfx_quality [
    if (= $arg1 4) [ // highest gfx quality
        fsaa 8
        minlod 200
        fpsrange 100 1000
        watersubdiv 2
        dynlight 16
        waterreflect 1
        dynshadow 40
        lighterror 1
        scorchttl 10000
        bulletholettl 10000
        bloodttl 10000
        gibttl 5000
        shotlinettl 75
    ] [
        if (= $arg1 3) [ // high
            fsaa 0
            minlod 100
            fpsrange 60 60
            watersubdiv 2
            dynlight 16
            waterreflect 1
            dynshadow 40
            lighterror 4
            scorchttl 10000
            bulletholettl 10000
            bloodttl 10000
            gibttl 5000
            shotlinettl 75            
        ] [
            if (= $arg1 2) [ // medium (DEFAULT)
                fsaa 0
                minlod 60
                fpsrange 30 40
                watersubdiv 4
                dynlight 16
                waterreflect 1
                dynshadow 40
                lighterror 4
                scorchttl 10000
                bulletholettl 10000
                bloodttl 10000
                gibttl 5000
                shotlinettl 75                
            ] [                 // low
                fsaa 0
                minlod 25
                fpsrange 20 25
                watersubdiv 16
                dynlight 0
                waterreflect 0
                dynshadow 0
                lighterror 10
                scorchttl 0
                bulletholettl 0
                bloodttl 0
                gibttl 0
                shotlinettl 0                
            ]
        ]
    ]
]
        
alias new_sound_quality [
    if (= $arg1 3) [ // high sound (DEFAULT)
        soundchans 64
        maxsoundsatonce 40
        footsteps 1
        bulletairsound 1
        bulletbouncesound 1
    ] [
        if (= $arg1 2) [ // medium
            soundchans 32
            maxsoundsatonce 10
            footsteps 1
            bulletairsound 0
            bulletbouncesound 0
        ] [                         // low
            soundchans 32
            maxsoundsatonce 5
            footsteps 0
            bulletairsound 0
            bulletbouncesound 0
        ]
    ]
]


newmenu "Are you sure you want to delete/reset ALL settings?"

menuitem "Yes, reset and quit now!" "resetcfg 1; quit"
menuitem "No, go back!" "showmenu settings"

newmenu controls

menuitem "Select an action and press ENTER to set a new key-binding"
menuitem ""
menuitemkeyinput "Forwards: "        forward
menuitemkeyinput "Left: "           left
menuitemkeyinput "Backwards: "       backward
menuitemkeyinput "Right: "          right
menuitemkeyinput "Attack: "         attack
menuitemkeyinput "Special action: " altaction
menuitemkeyinput "Jump: "           jump
menuitemkeyinput "Crouch: "         crouch
menuitemkeyinput "Reload: "         reload
menuitemkeyinput "Weapon menu: "    "showmenu weapons"
menuitemkeyinput "Next weapon: "     "universaldelta  1"
menuitemkeyinput "Previous weapon: " "universaldelta -1"
menuitemkeyinput "Show scores: "    showscores
menuitemkeyinput "Chat: "           saycommand
menuitemkeyinput "Team chat: "      "saycommand %"
menuitemkeyinput "Commandline: "    "saycommand /"
menuitemkeyinput "Console output: " toggleconsole
menuitemkeyinput "Screenshot: "     screenshot
menuitemkeyinput "Map overview: "   toggleshowmap
menuitemkeyinput "Drop CTF flag: "  dropflag
menuitem ""
menuitemcheckbox "Invert mouse: " "$invmouse" "invmouse $arg1"
menuitemslider "Mouse sensitivity: " 2 100 "($sensitivity)" 2 "" "sensitivity $arg1"
menuitem ""
menuitem "Reset ALL binds to defaults?" [ showmenu "Are you sure?" ]

alias resetbinds "exec config/resetbinds.cfg"

newmenu "Are you sure?"

menuitem "Yes, please reset all my binds to defaults" "resetbinds"
menuitem "No! Take me back!" "showmenu controls"

newmenu video

menuitemtextinput "Screen width:					" "$scr_w" "scr_w $arg1"
menuitemtextinput "Screen height:					" "$scr_h" "scr_h $arg1"

menuitemcheckbox "Fullscreen: " "$fullscreen" "fullscreen $arg1"

alias fsaa_valuemap "-1 2 4 8 16"
menuitemslider "Antialiasing (FSAA): " 0 4 "(findlist $fsaa_valuemap $fsaa)" 1 "off 2 4 8 16" "fsaa (at $fsaa_valuemap $arg1)"

menuitemslider "Vsync: " 0 1 "$vsync" 1 "off on" "vsync $arg1"

menuitemslider "Gamma: " 30  300 "$gamma"  5 "" "gamma $arg1"

alias minlod_valuemap "25 60 100 250"
menuitemslider "Minimal level of detail: " 25 250 "(findlist $minlod_valuemap $minlod)" 5 ["low" "decent" "good" "best"] "minlod (at $minlod_valuemap $arg1)"
menuitemcheckbox "Water reflection: " "$waterreflect" "waterreflect $arg1"

alias watersubdiv_valuemap "16 8 4 2 1"
menuitemslider "Water subdivision: " 0 4 "(findlist $watersubdiv_valuemap $watersubdiv)" 1 ["lowest quality" "low quality" "medium quality" "high quality" "highest quality"] "watersubdiv (at $watersubdiv_valuemap $arg1)"

menuitemcheckbox "Dynamic light: " "$dynlight" [ if (= $arg1 1) [ dynlight 16 ] [ dynlight 0 ] ]
menuitemcheckbox "Dynamic player shadows: " "$dynshadow" [ if (= $arg1 1) [ dynshadow 40 ] [ dynshadow 0 ] ]
menuitemslider "Light error: " 1 100 "$lighterror" 5 "" "lighterror $arg1"
menuitem "FPS range.." "showmenu fpsrange"

menuitem "" ""
menuitem "				[ OK ]				" resetgl


newmenu sound

menuitemslider "sound volume: " 0 255 "$soundvol" 16 "" "soundvol $arg1"
menuitemslider "music volume: " 0 255 "$musicvol" 16 "" "musicvol $arg1"
menuitemcheckbox "stereo:							" "$stereo" "stereo $arg1"
menuitemslider "channels: " 0 128 "$soundchans" 4 "" "soundchans $arg1"
menuitemslider "sounds at once: " 0 100 "$maxsoundsatonce" 2 "" "maxsoundsatonce $arg1"
menuitemslider "sound buffer length: " 128 4096 "$soundbufferlen" 128 "" "soundbufferlen $arg1"


newmenu fpsrange

menuitem "my 486 0wnz y00 (8-10)"        "fpsrange 8 10"
menuitem "slow machine (20-25)"          "fpsrange 20 25"
menuitem "sensible default (30-40)"      "fpsrange 30 40"
menuitem "fps whore (60-60)"             "fpsrange 60 60"
menuitem "I don't understand LOD (100+)" "fpsrange 100 1000"
