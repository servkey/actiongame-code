newmenu Crosshairs
menudirlist "packages/misc/crosshairs" "png" "loadcrosshair (concatword $arg1 .png)"


newmenu weapons

menumdl weapons/assault/menu mapmodel 75 12
menuitem "MTP-57 Assault Rifle"			"nextprimary 5;echo MTP-57 Assault Rifle chosen" "chmenumdl weapons weapons/assault/menu mapmodel 75 12"
menuitem "A-ARD/10 Submachine Gun"		"nextprimary 3;echo A-ARD/10 Submachine Gun chosen" "chmenumdl weapons weapons/subgun/menu mapmodel 75 12"
menuitem "Precision Tech AD-81 Sniper Rifle"     "nextprimary 4;echo Precision Tech AD-81 Sniper Rifle chosen" "chmenumdl weapons weapons/sniper/menu mapmodel 75 12"
menuitem "V-19 Combat Shotgun"			"nextprimary 2;echo V-19 Combat Shotgun chosen" "chmenumdl weapons weapons/shotgun/menu mapmodel 75 12"


newmenu CLA

menumdl playermodels/CLA/01 all 50 4
menuitem "Comandante"           "skin 0" "chmenumdl CLA playermodels/CLA/01 all 50 4"
menuitem "Psycho"               "skin 1" "chmenumdl CLA playermodels/CLA/02 all 50 4"
menuitem "Bomber"               "skin 2" "chmenumdl CLA playermodels/CLA/03 all 50 4"
menuitem "Ripper"               "skin 3" "chmenumdl CLA playermodels/CLA/04 all 50 4"


newmenu RVSF

menumdl playermodels/RVSF/01 all 50 4
menuitem "Alpha"            "skin 0" "chmenumdl RVSF playermodels/RVSF/01 all 50 4"
menuitem "Tango"            "skin 1" "chmenumdl RVSF playermodels/RVSF/02 all 50 4"
menuitem "Cobra"            "skin 2" "chmenumdl RVSF playermodels/RVSF/03 all 50 4"
menuitem "Viper"            "skin 3" "chmenumdl RVSF playermodels/RVSF/04 all 50 4"
menuitem "Swat"             "skin 4" "chmenumdl RVSF playermodels/RVSF/05 all 50 4"
menuitem "Police"           "skin 5" "chmenumdl RVSF playermodels/RVSF/06 all 50 4"


newmenu settings

menuinit [alias newteam (curteam)]
menuitemtextinput "Nickname:" "result $curname" "name $arg1"
menuitemslider "Team: " 0 1 "(curteam)" 1 "CLA RVSF" "alias newteam $arg1"
menuitem "" ""
menuitem "Select player skin.."          [showmenu (at [CLA RVSF] (curteam))]
menuitem "Select weapon.."        "showmenu weapons"
menuitem "" ""
menuitem "QUICK setup.."                                            [ showmenu "QUICK setup" ]
menuitem "Keyboard/mouse controls.."                                "showmenu controls"
menuitem "Video settings.."                                         "showmenu video"
menuitem "Sound settings.."                                         "showmenu sound"
menuitem "Game settings.."                                          "showmenu game"
menuitem "Head-Up display settings.."                               "showmenu HUD"
menuitem "" ""
menuitem "Reset all settings..."                          [ showmenu "Reset ALL settings?" ]
menuitem ""
menuitem "		 [ OK ] 		" [ team (at [CLA RVSF] $newteam) ]

newmenu "QUICK setup"

menuitemtextinput "Nickname:" 		"" "name $arg1"
menuitemtextinput "Resolution (Width):  " "$scr_w" "scr_w $arg1"
menuitemtextinput "Resolution (Height): " "$scr_h" "scr_h $arg1"
menuitemslider "Mouse sensitivity: " 2 100 "($sensitivity)" 2 "" "sensitivity $arg1"
menuitemslider "Graphics detail: " 1 4 "(getgfxq)" 1 "low medium good best" "new_gfx_quality $arg1"
menuitemslider "Sound detail: " 1 3 "(getsoundq)" 1 "low medium high" "new_sound_quality $arg1"
menuitem "" ""
menuitem "			 [ OK ] 			" "resetgl"

alias getgfxq [
    if (= $minlod 200) [ result 4 ] [
        if (= $minlod 100) [ result 3 ] [
            if (= $minlod 60) [ result 2 ] [ result 1 ]
        ]
    ]
]

alias getsoundq [
    if (= $maxsoundsatonce 10) [ result 3 ] [
        if (= $maxsoundsatonce 8) [ result 2 ] [ result 1 ]
    ]
]

alias new_gfx_quality [
    if (= $arg1 4) [ // highest gfx quality
        fsaa 8
        minlod 200
        fpsrange 100 1000
        watersubdiv 2
        dynlight 1
        waterreflect 1
        stencilbits 8
        dynshadow 40
        lighterror 1
        scorch 1
        bullethole 1
        blood 1
        gib 1
        shotline 1
    ] [
        if (= $arg1 3) [ // high
            fsaa 0
            minlod 100
            fpsrange 60 60
            watersubdiv 2
            dynlight 1
            waterreflect 1
            stencilbits 8
            dynshadow 40
            lighterror 4
            scorch 1
            bullethole 1
            blood 1
            gib 1
            shotline 1
        ] [
            if (= $arg1 2) [ // medium (DEFAULT)
                fsaa 0
                minlod 60
                fpsrange 30 40
                watersubdiv 4
                dynlight 1
                if (glext "GL_ARB_fragment_program") [
                    // make sure the 3D card is powerful enough for this
                    waterreflect 1
                ] [
                    waterreflect 0
                ]
                if (|| (glext "GL_EXT_stencil_two_side") (glext "GL_ATI_separate_stencil")) [
                    stencilbits 8
                ] [
                    stencilbits 0
                ]
                dynshadow 40
                lighterror 4
                scorch 1
                bullethole 1
                blood 1
                gib 1
                shotline 1
            ] [                 // low
                fsaa 0
                minlod 25
                fpsrange 20 25
                watersubdiv 16
                dynlight 0
                waterreflect 0
                stencilshadow 0
                stencilbits 0
                dynshadow 0
                lighterror 10
                scorch 0
                bullethole 0
                blood 0
                gib 0
                shotline 0
            ]
        ]
    ]
]
        
alias new_sound_quality [
    if (= $arg1 3) [ // high sound (DEFAULT)
        maxsoundsatonce 10
        footsteps 1
        bulletairsound 1
        bulletbouncesound 1
    ] [
        if (= $arg1 2) [ // medium
            maxsoundsatonce 8
            footsteps 1
            bulletairsound 0
            bulletbouncesound 1
        ] [                         // low
            maxsoundsatonce 5
            footsteps 0
            bulletairsound 0
            bulletbouncesound 0
        ]
    ]
]


newmenu "Reset ALL settings?"

menuitem "  Reset ALL settings and restart AC..." "resetcfg 1; quit"
menuitem "		Go back!		" "showmenu settings"

newmenu controls

menuitem "Select an action and press ENTER to set a new key-binding"
menuitem ""
menuitemkeyinput "Forwards: "        forward
menuitemkeyinput "Left: "           left
menuitemkeyinput "Backwards: "       backward
menuitemkeyinput "Right: "          right
menuitemkeyinput "Attack: "         attack
menuitemkeyinput "Special action: " altaction
menuitemkeyinput "Jump: "           jump
menuitemkeyinput "Crouch: "         crouch
menuitemkeyinput "Reload: "         reload
menuitemkeyinput "Weapon menu: "    "showmenu weapons"
menuitemkeyinput "Next weapon: "     "universaldelta  1"
menuitemkeyinput "Previous weapon: " "universaldelta -1"
menuitemkeyinput "Show scores: "    showscores
menuitemkeyinput "Chat: "           saycommand
menuitemkeyinput "Team chat: "      "saycommand %"
menuitemkeyinput "Commandline: "    "saycommand /"
menuitemkeyinput "Console output: " toggleconsole
menuitemkeyinput "Screenshot: "     screenshot
menuitemkeyinput "Map overview: "   toggleshowmap
menuitemkeyinput "Drop CTF flag: "  dropflag
menuitem ""
menuitemcheckbox "Invert mouse: " "$invmouse" "invmouse $arg1"
menuitemslider "Mouse sensitivity: " 2 100 "($sensitivity)" 2 "" "sensitivity $arg1"
menuitem ""
menuitem "		   [ Reset binds to defaults ]  		" [ showmenu "Are you sure you want to reset binds?" ]

alias resetbinds "exec config/resetbinds.cfg"

newmenu "Are you sure you want to reset binds?"

menuitem "	 Yes, reset all binds to defaults	" "resetbinds"
menuitem "		       No   		" "showmenu controls"

newmenu video

menuitemtextinput "Screen width: " "$scr_w" "scr_w $arg1"
menuitemtextinput "Screen height: " "$scr_h" "scr_h $arg1"

menuitemslider "View of field: " 90 120 "$fov" 2 "" "fov $arg1"
menuitemslider "Gamma: " 30  300 "$gamma"  5 "" "gamma $arg1"

alias fsaa_valuemap "-1 2 4 8 16"
menuitemslider "Antialiasing (FSAA): " 0 4 "(findlist $fsaa_valuemap $fsaa)" 1 "off 2 4 8 16" "fsaa (at $fsaa_valuemap $arg1)"
menuitemcheckbox "Vsync: " "$vsync" "vsync $arg1"
menuitemcheckbox "Fullscreen: " "$fullscreen" "fullscreen $arg1"


menuitem "" ""

alias minlod_valuemap "25 60 100 250"
menuitemslider "Minimal level of detail: " 0 3 "(findlist $minlod_valuemap $minlod)" 1 ["low" "decent" "good" "best"] "minlod (at $minlod_valuemap $arg1)"
menuitemslider "Light error: " 1 100 "$lighterror" 5 "" "lighterror $arg1"

alias watersubdiv_valuemap "16 8 4 2 1"
menuitemslider "Water subdivision: " 0 4 "(findlist $watersubdiv_valuemap $watersubdiv)" 1 ["lowest quality" "low quality" "medium quality" "high quality" "highest quality"] "watersubdiv (at $watersubdiv_valuemap $arg1)"
menuitemcheckbox "Water reflection: " "$waterreflect" "waterreflect $arg1"

getshadowq = [
    if (&& $stencilshadow (>= $stencilbits 8)) [result 2] [
        if $dynshadow [result 1] [result 0]
    ]
]

setshadowq = [
    if (= $arg1 2) [
        stencilshadow 40
        stencilbits 8
        dynshadow 40
    ] [
        if (= $arg1 1) [
            stencilshadow 0
            stencilbits 0
            dynshadow 40 
        ] [
            stencilshadow 0
            stencilbits 0
            dynshadow 0
        ]
    ]
]

menuitemcheckbox "Dynamic lights: " "$dynlight" [dynlight $arg1]
menuitemslider "Dynamic shadows: " 0 2 [getshadowq] 1 ["off" "blob" "stencil"] [setshadowq $arg1]

menuitem "FPS range.." "showmenu fpsrange"

menuitem "" ""
menuitemcheckbox "Bulletholes: " "$bullethole" "bullethole $arg1"
menuitemcheckbox "Scorch: " "$scorch" "scorch $arg1"
menuitemcheckbox "Blood: " "$blood" "blood $arg1; gib $arg1"


menuitem "" ""
menuitem "				[ Apply now! ]				" resetgl

newmenu fpsrange

menuitem "my 486 0wnz y00 (8-10)"        "fpsrange 8 10"
menuitem "slow machine (20-25)"          "fpsrange 20 25"
menuitem "sensible default (30-40)"      "fpsrange 30 40"
menuitem "fps whore (60-60)"             "fpsrange 60 60"
menuitem "I don't understand LOD (100+)" "fpsrange 100 1000"

newmenu sound

menuitemslider "Sound volume: " 0 255 "$soundvol" 16 "" "soundvol $arg1"
menuitemslider "Music volume: " 0 255 "$musicvol" 16 "" "musicvol $arg1"
menuitemslider "Sounds at once: " 0 100 "$maxsoundsatonce" 2 "" "maxsoundsatonce $arg1"
menuitemcheckbox "Footsteps: " "$footsteps" "footsteps $arg1"
menuitemcheckbox "Bullet hit: " "$bulletbouncesound" "bulletbouncesound $arg1"
menuitemcheckbox "Bullet near-miss: " "$bulletairsound" "bulletairsound $arg1"


alias new_poniesmode [
    if (= $arg1 1) [ 
        blood 0
        gibnum 0
        loop i (- 31 24) [ mutesound (+ (+ $i 24) 1) ] // mute pain sounds
        echo OMG!Ponies! mode enabled, we try to hide violent content now..    

    ] [ // violent
        blood 1
        gibnum 6
        unmuteallsounds
        echo OMG!Ponies! mode disabled..
    ]
]

alias getponiesmode [
    result (= $blood 0)
]


newmenu game

menuitemcheckbox "Auto weapon reload: " "$autoreload" "autoreload $arg1"
menuitemcheckbox "Show modes as acronyms: " "$modeacronyms" "modeacronyms $arg1"
menuitemcheckbox "Auto map download: " "$autogetmap" "autogetmap $arg1"
menuitemcheckbox "Show quit dialog when quitting: " "$askquit" "askquit = $arg1"
menuitemcheckbox "OMG!Ponies! mode: " "(getponiesmode)" "new_poniesmode $arg1"
menuitemslider "Player skins: " 0 2 "$teamdisplaymode" 1 [normal "colored vests" "full color"] "teamdisplaymode $arg1"


newmenu HUD

menuitemcheckbox "Show stats: " "(= $hidestats 0)" "hidestats (= $arg1 0)"
menuitemcheckbox "Show gun: " "$hudgun" "hudgun $arg1"
menuitemcheckbox "Show radar: " "(= $hideradar 0)" "hideradar (= $arg1 0)"
menuitemcheckbox "Show team icon: " "(= $hideteam 0)" "hideteam (= $arg1 0)"
menuitemcheckbox "Show votes: " "(= $hidevote 0)" "hidevote (= $arg1 0)"
menuitemcheckbox "Show icons: " "(= $hidehudequipment 0)" "hidehudequipment (= $arg1 0)"
menuitemcheckbox "Show console: " "(= $hideconsole 0)" "hideconsole (= $arg1 0)"
menuitemcheckbox "Show spectate info: " "(= $hidespecthud 0)" "hidespecthud (= $arg1 0)"
menuitemcheckbox "Show quick messages: " "(= $hidehudmsgs 0)" "hidehudmsgs (= $arg1 0)"
menuitemcheckbox "Show damage indicator: " "(= $hidedamageindicator 0)" "hidedamageindicator (= $arg1 0)"
menuitemcheckbox "Show crosshair team-sign: " "$crosshairteamsign" "crosshairteamsign $arg1"
menuitemslider "Crosshair size: " 0 50 "$crosshairsize" 5 "" "crosshairsize $arg1"
menuitem "Select crosshair..				"	  "showmenu Crosshairs"



