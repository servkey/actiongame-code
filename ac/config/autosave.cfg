checkinit mapstartalways [ if $autosavemap autoSaveCheck; numAutoSaves = 0 ]
checkinit onNewMap [ if $autosavemap autoSaveCheck; numAutoSaves = 0 ]

checkinit autosavemap 0           // Autosavemap system disabled by default
checkinit timeBetAutoSaves 300000 // Once every 5 minutes.
checkinit numAutoSaveBackups 12   // Make a map .bak every 12 autosaves by default

const autoSaveCheck [ sleep $timeBetAutoSaves [ doAutoSave ] 1 ]

const execAutoSave [
  += numAutoSaves 1
  oldmapbackups = $mapbackupsonsave
  if (|| (!= $numAutoSaveBackups $numAutoSaves) (= $numAutoSaveBackups 0)) [ mapbackupsonsave 0 ] [ numAutoSaves = 0 ] // Temporarily turn off map backups to avoid clutter.
  savemap (curmap 1)
  mapbackupsonsave $oldmapbackups
  echo (blue)The map was automatically saved. (white)Next auto-save in (divf $timeBetAutoSaves 60000) minutes.
]

const doAutoSave [
  if $autosavemap [
    if (&& (! (strstr $defaultmaps (curmap 1))) (strlen (curmap 1))) [
      if $editing execAutoSave [
        if (= $gamemode (coop -1)) execAutoSave
      ]
    ]
  ]
  autoSaveCheck
]