if (! (checkalias mapstartalways)) [ mapstartalways = "" ]
if (! (strstr $mapstartalways autoSaveCheck)) [ add2alias mapstartalways [ if $autosavemap autoSaveCheck; numAutoSaves = 0 ] ]

if (! (checkalias onNewMap)) [ onNewMap = "" ]
if (! (strstr $onNewMap autoSaveCheck)) [ add2alias onNewMap [ if $autosavemap autoSaveCheck; numAutoSaves = 0 ] ]

if (! (checkalias autosavemap)) [ autosavemap = 0 ]
if (! (checkalias timeBetAutoSaves)) [ timeBetAutoSaves = 300000 ] // Once every 5 minutes.
if (! (checkalias numAutoSaveBackups)) [ numAutoSaveBackups = 12 ]

const autoSaveCheck [ sleep $timeBetAutoSaves [ doAutoSave ] ]

const doAutoSave [
  if (&& (! (strstr $defaultmaps (curmap 1))) (! (strcmp (curmap 1) ""))) [
    if (|| $editing (= $gamemode (coop -1))) [
      if $autosavemap [
        += numAutoSaves 1
        oldmapbackups = $mapbackupsonsave
        if (|| (!= $numAutoSaveBackups $numAutoSaves) (= $numAutoSaveBackups 0)) [ mapbackupsonsave 0 ] [ numAutoSaves = 0 ] // Temporarily turn off map backups to avoid clutter.
        savemap (curmap 1)
        mapbackupsonsave $oldmapbackups
        echo (blue)The map was automatically saved. (white)Next auto-save in (divf $timeBetAutoSaves 60000) minutes.
      ]
    ]
  ]
  autoSaveCheck
]